# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk
pkgver=0.43
pkgrel=1
pkgdesc="GTK+ client library for SPICE"
arch=('x86_64')
url="https://www.spice-space.org/"
license=('LGPL-2.1-only')
depends=(
    'acl'
    'cairo'
    'gdk-pixbuf2'
    'glib2'
    'gst-plugins-base'
    'gst-plugins-good'
    'gstreamer'
    'gtk3'
    'json-glib'
    'libcacard'
    'libcap-ng'
    'libepoxy'
    'libjpeg-turbo'
    'libsasl'
    'libsoup3'
    'libusb'
    'libx11'
    'lz4'
    'openssl'
    'opus'
    'phodav'
    'pixman'
    'polkit'
    'spice-protocol'
    'usbredir'
    'wayland'
    'zlib'
)
makedepends=(
    'gi-docgen'
    'gobject-introspection'
    'glib2-devel'
    'meson'
    'python-six'
    'python-pyparsing'
    'usbutils'
    'vala'
    'wayland-protocols'
)
provides=("spice-glib=$pkgver" "spice-gtk3=$pkgver")
replaces=('spice-glib' 'spice-gtk3')
install=spice-gtk.install
source=("https://www.spice-space.org/download/gtk/$pkgname-$pkgver.tar.xz"{,.asc}
        "https://gitlab.freedesktop.org/spice/spice-gtk/-/commit/3f85c575b0fc7acd0c024ae0f331e09aa7ae0813.patch")
sha256sums=('cee26e5b2d22909f35b40a94398d1e863ca3962ee46494ca97aab206abc3203b'
            'SKIP'
            '1612a6e3795b0e2620ed8e6f4dd5c1057f1753a1e506a5764a545f7a3f5bd8e2')
validpgpkeys=(
  '206D3B352F566F3B0E6572E997D9123DE37A484F' # Victor Toso de Carvalho
  '87A9BD933F87C606D276F62DDAE8E10975969CE5' # Marc-André Lureau
)

prepare() {
  cd "$pkgname-$pkgver"
  # https://gitlab.freedesktop.org/spice/spice-gtk/-/work_items/204
  # Fixed on master, backporting until next release
  patch -p1 < ../3f85c575b0fc7acd0c024ae0f331e09aa7ae0813.patch
}

build() {
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
