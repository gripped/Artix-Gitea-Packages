# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-blosc2
pkgver=4.14.1
pkgrel=1
pkgdesc='Wrapper for the blosc2 compressor'
arch=(x86_64)
url='https://github.com/Blosc/python-blosc2'
license=(BSD-3-Clause)
depends=(
  blosc2
  glibc
  python
  python-msgpack
  python-ndindex
  python-numexpr
  python-numpy
  python-httpx
  python-h2
  python-pydantic
  python-rich
  python-threadpoolctl
)
makedepends=(
  cmake
  cython
  git
  ninja
  python-build
  python-installer
  python-scikit-build-core
  python-setuptools
)
checkdepends=(
  python-fsspec
  python-h5py
  python-psutil
  python-pytest
  python-pytest-asyncio
  python-requests
  python-aiohttp
)
optdepends=(
  'python-aiohttp: HTTP access through fsspec'
  'python-fsspec: filesystem URL support'
  'python-h5py: HDF5 support'
  'python-numba: Numba integration'
  'python-pandas: DataFrame conversion'
  'python-psutil: memory reporting in parquet-to-blosc2'
  'python-pyarrow: Arrow and Parquet support'
  'python-tensorflow: TensorFlow tensor serialization'
  'python-textual: b2view terminal viewer'
  'python-ujson: faster HDF5 index serialization'
)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  argh.patch
)
sha512sums=('07ee3f6035cfcb8ee4ad11d47368696b5cfe6970bb0d0d8a81deffd2e1e2173ea0b074901e78e7cfe2ced9b02d79865bf6d29f23a85c18c8f5aaf1029be3bf10'
            '88f486cd6385055da9bad586bc885ea852c4deb745fd429dc63e104cb579190115746932664ebad1b435df83cc1b6134b42ee5107db3dc92fb67cf7a3fd7acb0')
b2sums=('17cc4ebd5c143b12ca0e353d548605d919ad9c5b8369106334bfc43e12ad3e20f9e84da6ddebf135c2f27f5cccd22196bbbf46de2a6af16c3b9a8d96a1ae5667'
        '1595af3fe29e7410996a180d0456d276abae8f243eb6ec9497cb98979ccf03456f4c94e825375f70c2377987cc7d554e0df794e75c66830a05ecc7f4beb27864')

prepare() {
  cd "$pkgname"
  patch -p1 -i "$srcdir/argh.patch"
}

build() {
  cd $pkgname
  export CMAKE_ARGS="-DUSE_SYSTEM_BLOSC2=ON"
  # Preserve debug symbols and generated sources for makepkg
  python -m build --wheel --no-isolation \
    -Cinstall.strip=false \
    -Cbuild-dir=build
}

check() {
  cd $pkgname
  python -m venv venv-test --system-site-packages
  ./venv-test/bin/python -m installer dist/*.whl
  # Deselect tests failing since v3.4.0, not sure why
  # test_expand_dims: sys.getrefcount() behavior changed in Python 3.14
  # test_disk_cache_reuses_batch_column_prefixes: C-Blosc2 asserts clevel > 0 for VL blocks
  ./venv-test/bin/python -m pytest \
    --deselect tests/ctable/test_remote_ctable.py::test_disk_cache_reuses_batch_column_prefixes \
    --deselect tests/ndarray/test_resize.py::test_expand_dims \
    --deselect tests/ndarray/test_lazyexpr.py::test_broadcasting \
    --deselect tests/ndarray/test_lazyexpr.py::test_chain_expressions \
    --deselect tests/ndarray/test_lazyexpr.py::test_chain_persistentexpressions \
    --deselect tests/ndarray/test_reductions.py::test_broadcast_params \
    --deselect tests/ndarray/test_reductions.py::test_fast_path \
    --deselect tests/ndarray/test_reductions.py::test_save_version1 \
    --deselect tests/ndarray/test_reductions.py::test_save_version2 \
    --deselect tests/ndarray/test_reductions.py::test_save_version3 \
    --deselect tests/ndarray/test_reductions.py::test_save_version4
}

package() {
  cd $pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl

  # why are these files there?
  (
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgdir$site_packages"
  rm -vrf include lib share
  )

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
