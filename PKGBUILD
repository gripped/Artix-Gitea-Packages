# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-mock
pkgver=3.16.0
pkgrel=1
pkgdesc="Thin-wrapper around the mock package for easier use with py.test"
arch=('any')
license=('MIT')
url="https://github.com/pytest-dev/pytest-mock"
depends=(
  'python'
  'python-pytest'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=('python-pytest-asyncio')
source=("git+$url.git#tag=v$pkgver")
b2sums=('6bbf3b9e3a9b08646ebee6f6c8e7727797b472935c2dbed5fb892fbeea380e8d7150745e891de2d297bd4cc7c0a2f00acc8037da39efc5a9f1343613bf5e6f69')

prepare() {
  cd ${pkgname#python-}
  # Adapt tests to pytest 9.1
  git cherry-pick -n 1d42981a1577207db5919852f30ef08c97208496
}

build() {
  cd ${pkgname#python-}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}
  python -m venv tmpenv --system-site-packages
  tmpenv/bin/python -m installer dist/*.whl
  tmpenv/bin/python -m pytest
}

package() {
  cd ${pkgname#python-}
  python -m installer -d "$pkgdir" dist/*.whl
  install -vDm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
