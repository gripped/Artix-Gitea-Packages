# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Samuel Laurén <samuel.lauren@iki.fi>

pkgname=python-gssapi
pkgver=1.12.0
pkgrel=1
pkgdesc='Provides both low-level and high level wrappers around the GSSAPI C libraries'
url='https://github.com/pythongssapi/python-gssapi'
arch=('x86_64')
license=('ISC')
depends=(
  'glibc'
  'krb5'
  'python'
  'python-decorator'
)
makedepends=(
  'cython'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-k5test'
  'python-parameterized'
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('af8dc2a114ecf156156a0ea863f3fc6c5c9fd4f233767003faa52de2f85943ce6da87a8f5416b686e986b92e4c5440bd91b4905e40a981db94ef740f68ef762c')
b2sums=('d9905368baf076914af9990a9090dd7424e5165354e99125426e586d4144af11a900cdf452d15141e7fb1230a0ab81195b924c1243509fc5df9a62b15fa2ab9b')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  python -m unittest discover -v \
    --top-level-directory test-env/lib/python*/site-packages \
    --start-directory test-env/lib/python*/site-packages/gssapi/tests
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.txt
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
