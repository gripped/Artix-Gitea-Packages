# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=b43-fwcutter
pkgver=021
pkgrel=1
pkgdesc="firmware extractor for the b43 kernel module"
url="https://wireless.docs.kernel.org/en/latest/en/users/drivers/b43.html"
depends=('glibc')
license=('GPL-2.0-only')
arch=('x86_64')
source=("https://bues.ch/b43/fwcutter/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('c21e0ccf0d15e668ade31fe4d4c424ef6be006b85f63603b6f965f4c5a6f3121'
            'SKIP')
validpgpkeys=('757FAB7CED1814AE15B4836E5FB027474203454C') # Michael Büsch (Git tag signing key) <m@bues.ch>

build() {
	cd $pkgname-$pkgver
	make
}

package() {
	cd $pkgname-$pkgver
	install -D -m755 b43-fwcutter "$pkgdir"/usr/bin/b43-fwcutter
	install -D -m644 b43-fwcutter.1 "$pkgdir"/usr/share/man/man1/b43-fwcutter.1
}
