# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Carl Smedstad <carsme@archlinux.org>

pkgname=pegtl
pkgver=4.0.2
_commit=b60f6110abd37e502d202d13a6c565410c552c33
pkgrel=1
pkgdesc='Parsing Expression Grammar Template Library'
arch=('any')
url='https://github.com/taocpp/PEGTL'
license=('BSL-1.0')
makedepends=(
  'cmake'
  'git'
)
source=("$pkgname::git+$url#commit=$_commit?signed")
b2sums=('950b66753442150f6f315515183d0bb3f58a03c0c8545e987700096975137a4249127d14cefd759d686b4d1c01df9f6f1ee52834bb7982a23d18dc0f60c4812a')
validpgpkeys=(
  '3AC06334B62566C11A5912FB014C496DEC39EB21' # Daniel Frey <d.frey@gmx.de>
  '7FC5CCB763BC7C9141E834CCA8B7BB79E2DC1F33' # Dr. Colin Hirsch <github@colin-hirsch.net>
)

pkgver() {
  cd $pkgname
  git describe --tags
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DPEGTL_INSTALL_DOC_DIR=share/doc/$pkgname \
    -DPEGTL_INSTALL_CMAKE_DIR=lib/cmake/$pkgname \
    -DPEGTL_BUILD_EXAMPLES=OFF \
    -DPEGTL_BUILD_TESTS=ON
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build --prefix=/usr
  cd $pkgname
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
