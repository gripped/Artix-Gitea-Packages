# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-hiredis
_pkgname=hiredis-py
pkgver=3.4.2
pkgrel=1
pkgdesc='Non-blocking redis client for python'
arch=('x86_64')
url='https://pypi.org/project/hiredis/'
license=('MIT')
depends=(
  'glibc'
  'hiredis'
  'python'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=(
  "git+https://github.com/redis/hiredis-py.git#tag=v$pkgver"
  "$pkgname-use-system-hiredis.patch"
)
b2sums=('7e3d0a67fa6d4c4630e1fc713782a3d043534d3cef4a60ddc0cc1db1c4d1202f6ec63287faed3079aa647fd96eb9c29e0b9c805926e6668d43d81f8534364e36'
        '6a12a7237f742c02e852ac1823f7585c2246016c981b1b2b4436fa3859309cd2cb0efcb1c09f0d380d4f6430e61247d662922e53c690223a30138caa424f8e02')

prepare() {
  cd $_pkgname
  patch -Np1 < ../$pkgname-use-system-hiredis.patch
}

build() {
  cd $_pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/$site_packages" pytest
}

package() {
  cd $_pkgname
  python -m installer -d "$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
