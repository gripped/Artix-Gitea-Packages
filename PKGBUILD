# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Carl Smedstad <carsme@archlinux.org>

pkgname=rebar3
pkgver=3.27.1
pkgrel=1
pkgdesc="Erlang build tool that makes it easy to compile and test Erlang applications and releases"
arch=('any')
url="https://github.com/erlang/rebar3"
license=('Apache-2.0')
depends=(
  erlang-common_test
  erlang-core
  erlang-dialyzer
  erlang-edoc
  erlang-erl_interface
  erlang-eunit
  erlang-parsetools
)
makedepends=(git)
source=("git+$url.git#tag=$pkgver")
b2sums=('e4b7666566711647f60041e0232b3d9cf8e2ba217b597d49f976eba01abf873489eb3c623aacb4be006054c9def5c24dbc6790fb201a2dfd0abe8f9e90fad7a1')

build() {
  cd $pkgname
  ./bootstrap
}

check() {
  cd $pkgname
  ./rebar3 ct
}

package() {
  cd $pkgname
  install -vDm755 -t "$pkgdir/usr/bin" rebar3
  install -vDm644 -t "$pkgdir/usr/lib/erlang/lib/rebar-$pkgver/ebin" \
    _build/bootstrap/lib/rebar/ebin/*.beam \
    _build/bootstrap/lib/rebar/ebin/rebar.app

  install -vDm644 -t "$pkgdir/usr/share/bash-completion/completions" \
    apps/rebar/priv/shell-completion/bash/rebar3
  install -vDm644 -t "$pkgdir/usr/share/zsh/site-functions" \
    apps/rebar/priv/shell-completion/zsh/_rebar3
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_completions.d" \
    apps/rebar/priv/shell-completion/fish/rebar3.fish

  install -vDm644 -t "$pkgdir/usr/share/man/man1" manpages/rebar3.1
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" \
    README.md rebar.config.sample THANKS
}
