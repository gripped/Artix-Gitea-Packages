# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Antony Male <antony dot male at geemail dot com>>

pkgname=snappy
pkgver=1.3.1
pkgrel=1
pkgdesc='A fast compressor/decompressor library'
arch=('x86_64')
url="https://github.com/google/snappy"
license=('BSD-3-Clause')
depends=(
  'glibc'
  'libstdc++'
)
makedepends=(
  'clang'
  'cmake'
  'git'
  'gtest'
)
checkdepends=('zlib')
provides=('libsnappy.so')
source=(
  "git+${url}.git#tag=${pkgver}"
  "snappy.pc.in"
  "$pkgname-cmake_add_pkgconfig.patch"
  "$pkgname-use_system_gtest.patch"
  "$pkgname-reenable_rtti.patch"
)
b2sums=('f3bba61b2f84f60ee7258cc2fac5b08afbd10143d6978cb4f6665c517c0e852800e359e754cb05b62e74e9780391667c4924d2288497f0d8fc1be91f9f666b34'
        '01ded6bce04572813842383996941439fc7869b8f82cd5d13446c62c129c84d6cbb79d41a58c0076aa8687d22633e82a29d3169d33c9a9d6280848809177a89d'
        '5b4df9ff9534492bb4f14ace36353b7d2536a94e378f348d011a8578847e16946bc9e34e38657502e2732ea2953ef33d7d83fc24829412e0fbf9d4574bffcabe'
        '0c751bd1d57f70786c07c3579248db0d912d98cbcf2327eabbefe630dea45a1119db8f3bb190e718733f53b754080c4b335cd70ace944957ec81016e6087c8df'
        'd52b2ff4b9c74156bbe20c78741be36cac1c7696e9bece90e99a72ed24f79b9dd8bfe7c0c0138c3116f87a345f7437e80ad8841c43cf690ded7f5775b9349f96')

prepare() {
  cd $pkgname
  cp ../snappy.pc.in .
  patch -Np1 < ../$pkgname-cmake_add_pkgconfig.patch # https://bugs.archlinux.org/task/71246
  patch -Np1 < ../$pkgname-use_system_gtest.patch
  patch -Np1 < ../$pkgname-reenable_rtti.patch
}

build() {
  cmake -S $pkgname -B build \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -W no-dev \
    -DCMAKE_CXX_STANDARD=23 \
    -DBUILD_SHARED_LIBS=ON \
    -DSNAPPY_BUILD_BENCHMARKS=OFF
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" $pkgname/COPYING
}
